<!doctype html>
<html>
<head>
<meta charset="utf-8"/>
<meta name="viewport" content="width=device-width, initial-scale=1, maximum-scale=1, user-scalable=0"/>
<meta name="theme-color" content="#111"/>
<meta name="apple-mobile-web-app-capable" content="yes"/>
<meta name="apple-mobile-web-app-status-bar-style" content="black"/>
<meta name="msapplication-navbutton-color" content="#111"/>
<title>js13k2019</title>
<style>
html, body {
	margin: 0; padding: 0;
	background: #111;
	font: 5vw sans-serif; color: #555;
	overflow: hidden;
	-webkit-tap-highlight-color: rgba(0,0,0,0);
	-webkit-touch-callout: none;
	-webkit-user-select: none;
	-khtml-user-select: none;
	-moz-user-select: none;
	-ms-user-select: none;
	-ms-touch-action: none;
	user-select: none;
}
canvas {
	position: fixed;
	width: 100%;
	height: 100%;
}
</style>
</head>
<body>
<canvas id="Canvas">Sorry, this browser cannot render this content.</canvas>
<script id="LightVertexShader" type="x-shader/x-vertex">
attribute vec3 vertex;

uniform mat4 lightProjMat;
uniform mat4 lightModelViewMat;

void main() {
	gl_Position = lightProjMat * lightModelViewMat * vec4(vertex, 1.0);
}
</script>
<script id="LightFragmentShader" type="x-shader/x-fragment">
precision mediump float;

vec4 encodeFloat(float depth) {
	const vec4 bitShift = vec4(
		256 * 256 * 256,
		256 * 256,
		256,
		1.0
	);
	const vec4 bitMask = vec4(
		0,
		1.0 / 256.0,
		1.0 / 256.0,
		1.0 / 256.0
	);
	vec4 comp = fract(depth * bitShift);
	comp -= comp.xxyz * bitMask;
	return comp;
}

void main() {
	gl_FragColor = encodeFloat(gl_FragCoord.z);
}
</script>
<script id="OffscreenVertexShader" type="x-shader/x-vertex">
attribute vec3 vertex;

uniform mat4 projMat;
uniform mat4 modelViewMat;
uniform mat4 lightModelViewMat;
uniform mat4 lightProjMat;

varying mediump float z;
varying vec4 shadowPos;

const mat4 texUnitConverter = mat4(
	0.5, 0.0, 0.0, 0.0,
	0.0, 0.5, 0.0, 0.0,
	0.0, 0.0, 0.5, 0.0,
	0.5, 0.5, 0.5, 1.0
);

void main() {
	gl_Position = projMat * modelViewMat * vec4(vertex, 1.);
	z = gl_Position.z;
	shadowPos = texUnitConverter * lightProjMat * lightModelViewMat *
		vec4(vertex, 1.0);
}
</script>
<script id="OffscreenFragmentShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif

uniform float far;
uniform vec4 sky;
uniform vec4 color;
uniform sampler2D shadowDepthTexture;

varying mediump float z;
varying vec4 shadowPos;

float decodeFloat(vec4 c) {
	const vec4 bitShift = vec4(
		1.0 / (256.0 * 256.0 * 256.0),
		1.0 / (256.0 * 256.0),
		1.0 / 256.0,
		1
	);
	return dot(c, bitShift);
}

void main() {
	vec3 fragmentDepth = shadowPos.xyz;
	fragmentDepth.z -= 0.014;
	float amountInLight = 0.0;
	float texelDepth = decodeFloat(texture2D(shadowDepthTexture,
			fragmentDepth.xy));
	if (fragmentDepth.z < texelDepth) {
		amountInLight = 1.0;
	}
	float f = z / far;
	gl_FragColor = vec4(
		(1. - f) * color.rgb * (.75 + amountInLight * .3) + f * sky.rgb,
		color.a);
}
</script>
<script id="ScreenVertexShader" type="x-shader/x-vertex">
attribute vec2 vertex;
attribute vec2 texturePos;

varying vec2 textureUV;

void main() {
	gl_Position = vec4(vertex, 0., 1.);
	textureUV = texturePos;
}
</script>
<script id="ScreenFragmentShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif

varying vec2 textureUV;

uniform sampler2D offscreenTexture;

void main() {
	gl_FragColor = texture2D(offscreenTexture, textureUV.st);
}
</script>
<script src="src.js"></script>
</body>
</html>
