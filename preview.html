<!doctype html>
<html>
<head>
<meta charset="utf-8"/>
<meta name="viewport" content="width=device-width, initial-scale=1, maximum-scale=1, user-scalable=0"/>
<meta name="theme-color" content="#111"/>
<meta name="apple-mobile-web-app-capable" content="yes"/>
<meta name="apple-mobile-web-app-status-bar-style" content="black"/>
<meta name="msapplication-navbutton-color" content="#111"/>
<title>js13k2019</title>
<style>
html, body {
	margin: 0; padding: 0;
	background: #111;
	font: 5vw sans-serif; color: #555;
	overflow: hidden;
	-webkit-tap-highlight-color: rgba(0,0,0,0);
	-webkit-touch-callout: none;
	-webkit-user-select: none;
	-khtml-user-select: none;
	-moz-user-select: none;
	-ms-user-select: none;
	-ms-touch-action: none;
	user-select: none;
}
canvas {
	position: fixed;
	width: 100%;
	height: 100%;
}
</style>
</head>
<body>
<canvas id="Canvas">Sorry, this browser cannot render this content.</canvas>
<script id="LightVertexShader" type="x-shader/x-vertex">
attribute vec3 vertex;

uniform mat4 lightProjMat;
uniform mat4 lightModelViewMat;

void main() {
	gl_Position = lightProjMat * lightModelViewMat * vec4(vertex, 1.);
}
</script>
<script id="LightFragmentShader" type="x-shader/x-fragment">
precision mediump float;

void main() {
	const vec4 bitShift = vec4(16777216., 65536., 256., 1.);
	const vec4 bitMask = vec4(0., 1. / 256., 1. / 256., 1. / 256.);
	vec4 comp = fract(gl_FragCoord.z * bitShift);
	comp -= comp.xxyz * bitMask;
	gl_FragColor = comp;
}
</script>
<script id="OffscreenVertexShader" type="x-shader/x-vertex">
attribute vec3 vertex;

uniform mat4 projMat;
uniform mat4 modelViewMat;
uniform mat4 lightModelViewMat;
uniform mat4 lightProjMat;

varying mediump float z;
varying vec4 shadowPos;

const mat4 texUnitConverter = mat4(
	.5, .0, .0, .0,
	.0, .5, .0, .0,
	.0, .0, .5, .0,
	.5, .5, .5, 1.
);

void main() {
	gl_Position = projMat * modelViewMat * vec4(vertex, 1.);
	z = gl_Position.z;
	shadowPos = texUnitConverter * lightProjMat * lightModelViewMat *
		vec4(vertex, 1.);
}
</script>
<script id="OffscreenFragmentShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif

uniform float far;
uniform vec4 sky;
uniform vec4 color;
uniform sampler2D shadowDepthTexture;

varying mediump float z;
varying vec4 shadowPos;

const vec4 bitShift = vec4(1. / 16777216., 1. / 65536., 1. / 256., 1.);
float decodeFloat(vec4 c) {
	return dot(c, bitShift);
}

void main() {
	float depth = decodeFloat(texture2D(shadowDepthTexture, shadowPos.xy));
	float light = .75 + step(shadowPos.z - .007, depth) * .25;
	float fog = z / far;
	gl_FragColor = vec4(
		(1. - fog) * color.rgb * light + fog * sky.rgb,
		color.a);
}
</script>
<script id="ScreenVertexShader" type="x-shader/x-vertex">
attribute vec2 vertex;
attribute vec2 texturePos;

varying vec2 textureUV;

void main() {
	gl_Position = vec4(vertex, 0., 1.);
	textureUV = texturePos;
}
</script>
<script id="ScreenFragmentShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif

varying vec2 textureUV;

uniform sampler2D offscreenTexture;

void main() {
	gl_FragColor = texture2D(offscreenTexture, textureUV.st);
}
</script>
<script src="src.js"></script>
</body>
</html>
